; Crafting XP - Stentorious

; Requirements check
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
   MessageBoxEx "Crafting XP missing requirement!%rInstall xNVSE 6.4.0+."
   return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 250
	MessageBoxEx "Crafting XP missing requirement!%rInstall latest JohnnyGuitar NVSE."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 145
	MessageBoxEx "Crafting XP missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if eval IsModLoaded "DeadMoney.esm"
	AssignKeyword (EditorIDToFormID "NVDLC01VendingMachineRecipes") "CraftingXPBlacklist"
endif
if eval IsModLoaded "TaleOfTwoWastelands.esm"
	AssignKeyword (EditorIDToFormID "TTWAmmoPressRecipes") "CraftingXPBlacklist"
endif

; Anti exploit
if eval GetINIFloat_Cached "General:bAntiExploit" "Stentorious\CraftingXP.ini"

	AssignKeyword RecipeConversionSECtoECP "CraftingXPBlacklist"
	AssignKeyword RecipeConversionSECtoMFC "CraftingXPBlacklist"
	AssignKeyword RecipeConversionMFCtoSEC "CraftingXPBlacklist"
	AssignKeyword RecipeConversionMFCtoECP "CraftingXPBlacklist"
	AssignKeyword RecipeConversionECPtoSEC "CraftingXPBlacklist"
	AssignKeyword RecipeConversionECPtoMFC "CraftingXPBlacklist"

	if eval IsModLoaded "LonesomeRoad.esm"
		AssignKeyword (EditorIDToFormID "RecipeNVDLC04ConversionMissileToRocket") "CraftingXPBlacklist"
	endif
	if eval IsModLoaded "Supplemental Ammo Crafting.esp"
		AssignKeyword (EditorIDToFormID "RecipeBB") "CraftingXPBlacklist"
		AssignKeyword (EditorIDToFormID "RecipeBreakdownBB") "CraftingXPBlacklist"
	endif
	if eval IsModLoaded "JSawyer Ultimate.esp"
		AssignKeyword (EditorIDToFormID "NVJSawyerTinCanWorkbenchRcp") "CraftingXPBlacklist"
		SetRecipeRequiredSkillLevel (EditorIDToFormID "NVJSawyerThawNukaRcp") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "NVJSawyerThawSSRcp") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "NVJSawyerBreakdownRepairKitArmorRcp") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "NVJSawyerBreakdownRepairKitDummyRcp") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "NVJSawyerBreakdownRepairKitWeaponRcp") 1000
	endif
	if eval (IsModLoaded "sawyerbatty.esp" || IsModLoaded "Vigor.esp" || IsModLoaded "Lone Star.esm")
		AssignKeyword (EditorIDToFormID "JSawyerRecipeTinCan") "CraftingXPBlacklist"
	endif
	if eval IsModLoaded "Aid Addon.esp"
		SetRecipeRequiredSkillLevel (EditorIDToFormID "RecipeMedkitSmallBreakdown") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "RecipeMedkitMediumBreakdown") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "RecipeMedkitLargeBreakdown") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "RecipeMedKitImpBreakdown") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "RecipeMedKitSurvivalBreakdown") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "RecipeMedkitUltraBreakdown") 1000
		SetRecipeRequiredSkillLevel (EditorIDToFormID "RecipeDoctorsBagBreakdown") 1000
	endif

endif

Goo1.AuxVarSetFlt "*CraftXP_INI" (eval GetINIFloat_Cached "General:bValueBasedXP" "Stentorious\CraftingXP.ini") 0
Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (Floor (GetINIFloat_Cached "General:iMinimumXP" "Stentorious\CraftingXP.ini"))) 1
if eval Goo1.AuxVarGetFlt "*CraftXP_INI" 0
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:fValueBasedXPMul" "Stentorious\CraftingXP.ini")) 2
else
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "Crafting:fAid" "Stentorious\CraftingXP.ini")) 2
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "Crafting:fAmmo" "Stentorious\CraftingXP.ini")) 3
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "Crafting:fBreakdown" "Stentorious\CraftingXP.ini")) 4
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "Crafting:fFood" "Stentorious\CraftingXP.ini")) 5
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "Crafting:fWeapon" "Stentorious\CraftingXP.ini")) 6
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "Crafting:fMisc" "Stentorious\CraftingXP.ini")) 7
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "SupplementalAmmoCrafting:fAmmoPistol" "Stentorious\CraftingXP.ini")) 8
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "SupplementalAmmoCrafting:fAmmoRifle" "Stentorious\CraftingXP.ini")) 9
	Goo1.AuxVarSetFlt "*CraftXP_INI" (GetMaxOf 0 (GetINIFloat_Cached "SupplementalAmmoCrafting:fAmmoShotgun" "Stentorious\CraftingXP.ini")) 10
endif

; Clear INI cache
CloseFileSO "Stentorious\CraftingXP.ini" 0

; Event handlers
SetOnCraftingEventHandler (CompileScript "CraftingXP/Main.gek") 1
SetOnMenuOpenEventHandler (begin function {int iMenuID}
	if eval GetActiveMenuMode != iMenuID
		return
	endif
	Goo1.AuxVarSetFlt "*CraftXP_Stat" (GetPCMiscStat "Items Crafted") 0
	Goo1.AuxVarSetFlt "*CraftXP_Stat" 0 1
end) 1 1077
SetOnMenuCloseEventHandler (begin function {int iMenuID}
	if eval Goo1.AuxVarGetFlt "*CraftXP_Stat" 1 > 0
		RewardXP (GetMaxOf (Goo1.AuxVarGetFlt "*CraftXP_INI" 1) (Flr ((Goo1.AuxVarGetFlt "*CraftXP_Stat" 1) + 0.5)))
	endif
end) 1 1077
